plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id "org.flywaydb.flyway" version "5.2.4" apply false
    id "com.github.spotbugs" version "4.0.2"
    id 'jacoco'
    id 'checkstyle'
}

configure(allprojects) { project ->
    apply plugin: "io.spring.dependency-management"

    group 'ru.megafon'
    version '1.0-SNAPSHOT'

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE'
        }

        dependencies {
            dependency "net.rakugakibox.spring.boot:orika-spring-boot-starter:1.9.0"
            dependency "org.testcontainers:postgresql:1.14.1"
            dependency "org.flywaydb:flyway-core:6.4.0"
            dependency "io.springfox:springfox-swagger-ui:2.9.2"
            dependency "io.springfox:springfox-swagger2:2.9.2"
        }

        repositories {
            mavenCentral()
        }
    }
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'

    checkstyle {
        toolVersion = '8.31'
    }

    spotbugs {
        ignoreFailures = true
    }

    jacoco {
        toolVersion = "0.8.5"
        reportsDir = file("$buildDir/jacocoReportDir")
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
